// ===============================================================================
//                           潇然系统部署背景插件 PECMD 版
// ===============================================================================

// 获取分辨率
SCRN ScrW,ScrH

// 重设标题
ENVI Name=系统正在安装/部署/优化中，请勿操作电脑！
WRIT "%SystemDrive%\Windows\Setup\wallname.txt",$1,%Name%

// 读取外置壁纸
IFEX "%SystemDrive%\Windows\Setup\Set\BG.jpg" ,FILE "%SystemDrive%\Windows\Setup\Set\BG.jpg"=>wall.jpg
IFEX ! "wall.jpg" ,FILE "%SystemDrive%\Windows\Web\Wallpaper\Windows\img0.jpg"=>wall.jpg

// 主窗口，启动！
CALL WIN1

_SUB WIN1,L0T0W%ScrW%H%ScrH%,潇然系统部署背景插件,,imageres.dll#22,#:,,-ntab -nfocus -na -trap
    // ENVI @this.Enable=0
    ENVI @this.Font=12:微软雅黑
    CALL DRAW
    TIME ,3000,CALL 任务栏监控
    TIME ,5000,CALL 刷新分辨率
    TIME ,500,CALL 刷新标题
_END

_SUB DRAW
    ENVI 任务栏=关
    CALL 隐藏任务栏
    // 设置窗口位置以满屏显示
    ENVI @this.POS=0:0:%ScrW%:%ScrH%:1
    
    // 背景图片
    ENVI @BKG.*del=
    IMAG BKG,W%ScrW%H%ScrH%,wall.jpg,
    
    // 任务标题
    ENVI @LABE1.*del=
    LABE -center -trans LABE1,L0T30W%ScrW%H30,%Name%,,0xffffff,24,

    // 任务副标题
    ENVI @LABE2.*del=
    LABE -center -trans LABE2,L0T80W%ScrW%H30,%Name%,,0xffffff,14,

    
    // 进度条
    // CALC 进度条上=%ScrH%-16
    // PBAR ,L0T%进度条上%W%ScrW%H16,75,
    
    CALL SYSINFO
    // 读取镜像状态
    REGI .HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Setup\State\ImageState,ImageState
    IFEX |%ImageState%=IMAGE_STATE_COMPLETE,CALL WBTN

_END

_SUB WBTN
    ENVI 任务栏=关
    CALL 隐藏任务栏
    ENVI @BKG.Visible=1
    IFEX ! "%SystemDrive%\Windows\Setup\Set\" ,
    {
        // 关闭按钮
        CALC 关=%ScrW%-51-51
        ENVI @关.*del=
        LABE -center -vcenter 关,L%关%T1W50H30,×,KILL \,0xffffff#0xf0f0f0#0xffffff#0x0000cd,16:宋体
    }

    // 隐藏按钮
    CALC 隐=%ScrW%-51
    ENVI @隐.*del=
    LABE -center -vcenter -trans 隐,L%隐%T1W50H30,-,CALL WBTN_trans,0xffffff#0xf0f0f0#0xffffff#0xd2d2d2,16:宋体
_END

_SUB WBTN_trans

    // 删除图片
    ENVI @BKG.Visible=0
    
    ENVI 任务栏=开
    CALL 显示任务栏
    
    // 恢复隐藏按钮
    CALC 隐=%ScrW%-51
    ENVI @隐.*del=
    LABE -center -vcenter 隐,L%隐%T1W50H30,■,CALL WBTN,0xffffff#0xf0f0f0##0xd2d2d2,16:宋体
_END

_SUB SYSINFO
    // 快捷按钮
    CALC 快捷左=%ScrW%-90
    CALC 快捷上=%ScrH%-90
    
    ITEM SCUT1,L%快捷左%T%快捷上%W40H40,,EXEC TASKMGR.EXE,TASKMGR.EXE#0
    ENVI @SCUT1.trans=2
    CALC 快捷上=%快捷上%-10-48

    ITEM SCUT2,L%快捷左%T%快捷上%W40H40,,EXEC CMD.EXE,CMD.EXE#0
    ENVI @SCUT2.trans=2
    CALC 快捷上=%快捷上%-10-48
    
    ITEM SCUT3,L%快捷左%T%快捷上%W40H40,,EXEC MMC COMPMGMT.MSC,MMC.EXE#0
    ENVI @SCUT3.trans=2
    CALC 快捷上=%快捷上%-10-48
    
    ITEM SCUT4,L%快捷左%T%快捷上%W40H40,,EXEC PECMD SCRN -gui,shell32.dll#139
    ENVI @SCUT4.trans=2
    CALC 快捷上=%快捷上%-10-48
    
    
    // 信息区域
    CALC 信息左=90
    CALC 信息上=%ScrH%-90-210

    REGI .HKLM\SYSTEM\ControlSet001\Control\ComputerName\ComputerName\ComputerName,计算机名
    LABE -left -trans ,L%信息左%T%信息上%W500H20,计算机名：%计算机名%,,0xffffff,14
    CALC 信息上=%信息上%+30
    
    REGI .HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\ProductName,产品名称
    REGI .HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\DisplayVersion,显示版本
    IFEX |%显示版本%=,REGI .HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\CSDVersion,显示版本
MESS %显示版本%
    ENVI ?系统架构=PEBIT,%windir%\system32
    LABE -left -trans ,L%信息左%T%信息上%W500H20,版本：%产品名称% %显示版本% %系统架构%,,0xffffff,14
    CALC 信息上=%信息上%+30

    EXEC*N VER=!CMD.EXE /C VER
    MSTR * VER=<-1>VER
    MSTR * VER=-2,0,VER
    REGI .HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\BuildLabEx,BuildLabEx
    MSTR * -delims:. SPBuild=<2>BuildLabEx
    IFEX #%SPBuild%>1,ENVI VER=%VER%.%SPBuild%
    REGI .HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\BuildLabEx,内部版本
    LABE -left -trans ,L%信息左%T%信息上%W500H20,内部版本：%VER%,,0xffffff,14
    CALC 信息上=%信息上%+30

    REGI .HKLM\HARDWARE\DESCRIPTION\System\CentralProcessor\0\ProcessorNameString,处理器
    LABE -left -trans ,L%信息左%T%信息上%W500H20,处理器：%处理器%,,0xffffff,14
    CALC 信息上=%信息上%+30
    
    TEAM FIND MEM=?,内存|CALC 内存=%内存%/1024#2
    TEAM FIND %SystemDrive%\=?,系统盘|CALC 系统盘=%系统盘%/1024^3#2

    LABE -left -trans ,L%信息左%T%信息上%W500H20,内存：%内存% GB    系统盘(%SystemDrive%)可用：%系统盘% GB,,0xffffff,14
    CALC 信息上=%信息上%+30
    
    REGI .HKLM\HARDWARE\DESCRIPTION\System\BIOS\BaseBoardManufacturer,主板厂商
    LABE -left -trans ,L%信息左%T%信息上%W500H20,主板厂商：%主板厂商%,,0xffffff,14
    CALC 信息上=%信息上%+30
    
    REGI .HKLM\HARDWARE\DESCRIPTION\System\BIOS\BaseBoardProduct,主板型号
    LABE -left -trans ,L%信息左%T%信息上%W500H20,主板型号：%主板型号%,,0xffffff,14
    CALC 信息上=%信息上%+30
    
    LABE -left -trans ,L%信息左%T%信息上%W500H20,分辨率：%ScrW% × %ScrH%,,0xffffff,14
    CALC 信息上=%信息上%+30
    
    //LABE -left -trans ,L10T10W500H20,计算最终高度：%信息上%,,0xffffff,14
_END

_SUB 刷新分辨率
    SCRN ScrWnew,ScrHnew
    IFEX [#%ScrW%<>%ScrWnew% | #%ScrH%<>%ScrHnew%],
    {
        EXEC --nowait PECMD LOAD %CurFile%
        KILL \
    }
_END

_SUB 刷新标题
    READ "%SystemDrive%\Windows\Setup\wallname.txt",1,NameNew
    IFEX |%NameNew%<>%Name%,
    {
        ENVI Name=%NameNew%
        ENVI @LABE1=%Name%
    }
    IFEX |%NameNew%=exit,EXIT FILE
_END

_SUB 任务栏监控
    IFEX |%任务栏%=开,CALL 显示任务栏
    IFEX |%任务栏%=关,CALL 隐藏任务栏
_END

_SUB 隐藏任务栏
    find --class:Shell_TrayWnd --wid* &TASKBARID,
    ENVI @@visible=%&TASKBARID%:0
    SET &FUXWID=0
    FIND --class:FirstUXWndClass --wid* &FUXWID,
    IFEX |%&FUXWID%<>0,
    {
        FIND --wid*@%&FUXWID% &FUXWS
        MSTR FUXT=<8>%&FUXWS%
        ENVI @@visible=%&FUXWID%:0
        IFEX |%LABE2%<>%FUXT%,ENVI @LABE2=%FUXT%
    }
_END

_SUB 显示任务栏
    find --class:Shell_TrayWnd --wid* &TASKBARID,
    ENVI @@visible=%&TASKBARID%:1
_END

